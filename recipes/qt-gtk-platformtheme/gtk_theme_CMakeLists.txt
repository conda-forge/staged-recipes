project(QtGTKPlatformTheme)

cmake_minimum_required(VERSION 3.16)

option(USE_QT6 "Use Qt6 instead of Qt5" OFF)

if (USE_QT6)
    set(QT_MIN_VERSION "6.2.0")
    set(CMAKE_CXX_STANDARD 17)
    set(CMAKE_CXX_STANDARD_REQUIRED ON)
    set(QT_VERSION_MAJOR "6")
else()
    set(QT_MIN_VERSION "5.15.2")
    set(CMAKE_CXX_STANDARD 14)
    set(CMAKE_CXX_STANDARD_REQUIRED ON)
    set(QT_VERSION_MAJOR "5")
endif()

find_package(Qt${QT_VERSION_MAJOR} ${QT_MIN_VERSION} CONFIG REQUIRED COMPONENTS
    Core
    DBus
    Gui
    Widgets
    ThemeSupport
)

if (NOT QT_PLUGINS_DIR)
    if (NOT USE_QT6)
        get_target_property(REAL_QMAKE_EXECUTABLE ${Qt5Core_QMAKE_EXECUTABLE}
                            IMPORTED_LOCATION)
        execute_process(COMMAND "${REAL_QMAKE_EXECUTABLE}" -query QT_INSTALL_PLUGINS
                        OUTPUT_VARIABLE QT_PLUGINS_DIR
                        ERROR_QUIET OUTPUT_STRIP_TRAILING_WHITESPACE)
    else()
        set(QT_PLUGINS_DIR ${QT6_INSTALL_PLUGINS})
    endif()
endif()

find_package(PkgConfig REQUIRED)
pkg_check_modules(GTK+3 REQUIRED IMPORTED_TARGET gtk+-3.0)


find_package(X11 REQUIRED)

set(gtk_theme_SRCS
    main.cpp
    qgtk3dialoghelpers.cpp
    qgtk3dialoghelpers.h
    qgtk3menu.cpp
    qgtk3menu.h
    qgtk3theme.cpp
    qgtk3theme.h
)

add_library(qgtk3 MODULE ${gtk_theme_SRCS})
target_compile_definitions(qgtk3 PRIVATE -DGDK_VERSION_MIN_REQUIRED=GDK_VERSION_3_6)

target_link_libraries(qgtk3
    PkgConfig::GTK+3
    Qt${QT_VERSION_MAJOR}::Core
    Qt${QT_VERSION_MAJOR}::CorePrivate
    Qt${QT_VERSION_MAJOR}::Gui
    Qt${QT_VERSION_MAJOR}::GuiPrivate
)

if (NOT USE_QT6)
    target_link_libraries(qgtk3 Qt5::ThemeSupportPrivate)
endif()
set_property(TARGET qgtk3 PROPERTY AUTOMOC ON)

install(TARGETS qgtk3 DESTINATION ${QT_PLUGINS_DIR}/platformthemes)

